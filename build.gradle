plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id 'dev.architectury.loom' version '1.10-SNAPSHOT' apply(false)
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    project.archivesBaseName = "${rootProject.archives_base_name}-${project.name}"

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered {
            it.mappings("net.fabricmc:yarn:${rootProject.yarn_mappings}:v2")
            it.mappings("dev.architectury:yarn-mappings-patch-neoforge:1.21+build.4")
        }
    }
}

// Ferry for publishing; this has to be after arch dev plugin apply and before subproject version is set.
apply from: 'https://raw.githubusercontent.com/TerraformersMC/GradleScripts/3.0-beta/ferry-mpp.gradle'

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    group = rootProject.maven_group
    version = rootProject.version

    repositories {
        mavenCentral()
        maven {
            name = 'TerraformersMC'
            url = 'https://maven.terraformersmc.com/'
        }

        // for TerraBlender
        maven {
            url = 'https://maven.minecraftforge.net/'
        }

        // for Moderner Beta
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Modrinth'
                    url = 'https://api.modrinth.com/maven'
                }
            }
            filter {
                includeGroup "maven.modrinth"
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.getRelease().set(21)
    }

    java {
        withSourcesJar()
    }
}
