plugins {
    id 'java'
    id "xyz.wagyourtail.unimined" version "1.2.9" apply false
}

subprojects {
    apply plugin: "xyz.wagyourtail.unimined"
    apply plugin: "java"
    apply plugin: 'maven-publish'

    project.archivesBaseName = "${rootProject.archives_base_name}-${project.name}"

    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        mavenCentral()
        maven {
            name = 'TerraformersMC'
            url = 'https://maven.terraformersmc.com/'
        }

        // for TerraBlender
        maven {
            url = 'https://maven.minecraftforge.net/'
        }

        // for Modern Beta
        exclusiveContent {
            forRepository {
                maven {
                    url "https://cursemaven.com"
                }
            }
            filter {
                includeGroup "curse.maven"
            }
        }
    }

    dependencies {
    }

    configurations {
        modCompileOnly
        compileOnly.extendsFrom modCompileOnly
        modApi
        api.extendsFrom modApi
    }

    unimined.minecraft(sourceSets.main, true) {
        version rootProject.minecraft_version

        mappings {
            intermediary()
            yarn(rootProject.yarn_mappings)
            devNamespace "yarn"
            devFallbackNamespace "intermediary"
        }

        mods {
            remap(configurations.modCompileOnly) {
            }

            remap(configurations.modApi)

            // this is basically just a shortcut for `remap(configurations.modImplementation)`
            modImplementation {
                // you can do this is mods have the wrong access widener mapping, but it may break runs
                catchAWNamespaceAssertion()
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.getRelease().set(17)
    }

    java {
        withSourcesJar()
    }
}

// Ferry for publishing; this has to be after arch dev plugin apply and before subproject version is set.
//apply from: 'https://raw.githubusercontent.com/TerraformersMC/GradleScripts/3.0-beta/ferry-mpp.gradle'
