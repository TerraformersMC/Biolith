plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

// Ferry for publishing
apply from: 'https://raw.githubusercontent.com/TerraformersMC/GradleScripts/3.0-beta/ferry-mpp.gradle'

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig("biolith.mixins.json", "biolith.forge.mixins.json")
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    //modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    // Forge does not provide MixinExtras
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:${rootProject.mixinextras_version}"))
    implementation(include("io.github.llamalad7:mixinextras-forge:${rootProject.mixinextras_version}"))

    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    // TerraBlender so we can use its API
    modCompileOnly "com.github.glitchfiend:TerraBlender-forge:${rootProject.terrablender_version}"

    // Terraform Surfaces API for OpenSimplexNoise
    //include(modImplementation("com.terraformersmc.terraform-api:terraform-surfaces-api-v1:${rootProject.terraform_surfaces_api_version}"))
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publish {
    onlyIf {
        rootProject.enabled_platforms.split(",").contains(project.projectDir)
    }
    onlyIf {
        checkVersion(archivesBaseName, version)
    }

    curseforge_game_versions = "${rootProject.curseforge_game_versions}, Forge"
    modrinth_mod_loaders = "forge"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId "${rootProject.maven_group}"
            artifactId "${project.archivesBaseName}"
            version "${project.version}"

            // Exclude TerraBlender from maven deps so Loom does not load it during builds
            // and tests of other projects using our API (unless they configure it to).
            pom.withXml {
                asNode().dependencies.dependency.each { dep ->
                    if (dep.artifactId.last().value().last() in ["TerraBlender-fabric", "TerraBlender-forge", "TerraBlender-neoforge"]) {
                        assert dep.parent().remove(dep)
                    }
                }
            }
        }
    }

    setupRepositories(repositories)
}

static void setupRepositories(RepositoryHandler repositories) {
    //repositories.mavenLocal() // uncomment for testing
    def ENV = System.getenv()
    if (ENV.MAVEN_URL) {
        repositories.maven {
            url ENV.MAVEN_URL
            credentials {
                username ENV.MAVEN_USERNAME
                password ENV.MAVEN_PASSWORD
            }
        }
    }
}

ext.checkVersion = { archivesBaseName, version ->
    try {
        def xml = new URL("https://maven.terraformersmc.com/com/terraformersmc/$archivesBaseName/maven-metadata.xml").text
        def metadata = new XmlSlurper().parseText(xml)
        def versions = metadata.versioning.versions.version*.text()
        if (versions.contains(version)) {
            println "$archivesBaseName version $version will not be published; already exists"
            return false
        } else {
            println "$archivesBaseName version $version will be published"
            return true
        }
    } catch (FileNotFoundException ignored) {
        println "$archivesBaseName is not on maven yet"
        return true
    }
}